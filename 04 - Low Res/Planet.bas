 1  DEF  FN MOD(A) =  INT ((A / 4 -  INT (A / 4)) * 4 + .05) *  SGN (A / 4)
 2 PI = 3.14159265
 3  HGR2 
 10  DATA  0,8,2,10,12,4,14,6,3,11,1,9,15,7,13,5
 20  DIM BAYER(15)
 30  FOR I = 0 TO 15
 40  READ BAYER(I)
 50  NEXT I
 60 LX =  RND (1)
 70 LY =  RND (1)
 80 LZ =  RND (1)
 90 LM =  SQR (LX ^ 2 + LY ^ 2 + LZ ^ 2)
 100 LX = LX / LM
 110 LY = LY / LM
 120 LZ = LZ / LM
 130 AMBIENT = 1 / 16
 140 WIDTH = 280
 150 HEIGHT = 192
 160 XRING = WIDTH / 3
 170 YRING = HEIGHT / 8
 171 RS = PI
 172 RF = 2 * PI
 173 RI = PI / 16
 174  FOR SA = 0 TO 10 STEP 2
 180  GOSUB 2000
 181  NEXT SA
 190  FOR Y = HEIGHT / 4 TO 3 * HEIGHT / 4 - 1
 200  FOR X = WIDTH / 2 - HEIGHT / 4 TO WIDTH / 2 + HEIGHT / 4 - 1
 210 XSCALED = (X - WIDTH / 2) / (HEIGHT / 4)
 220 YSCALED = (Y - HEIGHT / 2) / (HEIGHT / 4)
 230  IF XSCALED ^ 2 + YSCALED ^ 2 >  = 1 THEN 340
 240 Z = 1 - (XSCALED ^ 2 + YSCALED ^ 2)
 250 DOT = LX * XSCALED + LY * YSCALED + LZ * Z
 260  IF DOT < 0 THEN DOT = 0
 270 XMOD =  FN MOD(X)
 280 YMOD =  FN MOD(Y)
 290  IF (DOT + AMBIENT) * 16 > BAYER(XMOD + YMOD * 4) THEN 320
 300  HCOLOR= 0
 310  GOTO 330
 320  HCOLOR= 3
 330  HPLOT X,Y
 340  NEXT X
 350  NEXT Y
 351 RS = 0
 352 RF = PI
 353  FOR SA = 0 TO 10 STEP 2
 360  GOSUB 2000
 361  NEXT SA
 370  END 
 2000  HCOLOR= 3
 2001  FOR I = RS TO RF STEP RI
 2010  HPLOT  COS (I) * (XR + SA) + WIDTH / 2, SIN (I) * (YR + SA / 2) + HEIGHT / 2 TO  COS (I - RI) * (XR + SA) + WIDTH / 2, SIN (I - RI) * (YR + SA / 2) + HEIGHT / 2
 2020  NEXT I
 2030  RETURN 
